import java.util.regex.Matcher
import java.util.regex.Pattern

/*
 *    Calendula - An assistant for personal medication management.
 *    Copyright (C) 2016 CITIUS - USC
 *
 *    Calendula is free software; you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation; either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    This program is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with this software.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}
apply plugin: 'com.android.application'
apply plugin: 'idea'



repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "http://dl.bintray.com/journeyapps/maven" }
    maven { url 'https://jitpack.io' }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// read keystore for app signing
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = null
if (keystorePropertiesFile.exists()) {
    keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def getCurrentFlavor() {
    Gradle gradle = getGradle()
    String  tskReqStr = gradle.getStartParameter().getTaskRequests().toString()

    Pattern pattern;

    if( tskReqStr.contains( "assemble" ) )
        pattern = Pattern.compile("assemble(\\w+)(Release|Debug)")
    else
        pattern = Pattern.compile("generate(\\w+)(Release|Debug)")

    Matcher matcher = pattern.matcher( tskReqStr )

    if( matcher.find() )
        return matcher.group(1).toLowerCase()
    else
    {
        println "NO MATCH FOUND"
        return "";
    }
}

def currentFlavor = getCurrentFlavor()
print currentFlavor

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    return formattedDate
}

def getCustomProguardFiles() {
    return fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 29
        versionName "2.5-SNAPSHOT"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        applicationId "es.usc.citius.servando.calendula"
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties != null ? keystoreProperties['keyAlias'] : null
            keyPassword keystoreProperties != null ? keystoreProperties['keyPassword'] : null
            storeFile keystoreProperties != null ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties != null ? keystoreProperties['storePassword'] : null
        }
    }


    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles getCustomProguardFiles()
            testProguardFiles getDefaultProguardFile('proguard-android.txt')
            testProguardFiles getCustomProguardFiles()
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
        }
        beta {
            minifyEnabled false
            versionNameSuffix "-build" + getDate()
        }
        alpha {
            minifyEnabled false
            applicationIdSuffix ".alpha"
            signingConfig signingConfigs.release
            versionNameSuffix "-" + getDate()
        }
    }

    flavorDimensions "buildType"

    productFlavors {
        develop {
            applicationIdSuffix ".dev"
            dimension "buildType"
        }
        product {
            dimension "buildType"
        }
        ci {
            dimension "buildType"

        }
        pharma {
            dimension "buildType"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }
    compileOptions.incremental = false
}

dependencies {

    compile 'com.google.code.gson:gson'
    compile 'com.nispok:snackbar:2.9.0'
    compile 'com.doomonafireball.betterpickers:library:1.5.5'
    compile 'com.github.filippudak.progresspieview:library:1.0.4'
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'

    // support
    compile "com.android.support:appcompat-v7:$libVersions.android.support"
    compile "com.android.support:cardview-v7:$libVersions.android.support"
    compile "com.android.support:recyclerview-v7:$libVersions.android.support"
    compile "com.android.support:palette-v7:$libVersions.android.support"
    compile "com.android.support:support-v4:$libVersions.android.support"
    compile "com.android.support:design:$libVersions.android.support"
    compile "com.android.support:support-annotations:$libVersions.android.support"
    // view/resource binding
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    // round image view
    compile 'com.makeramen:roundedimageview:1.5.0'

    // Time and date handling
    compile 'joda-time:joda-time:2.3'
    compile 'com.fatboyindustrial.gson-jodatime-serialisers:gson-jodatime-serialisers:1.1.0'

    // Floating action button
    compile 'com.melnykov:floatingactionbutton:1.1.0'

    // calendar view
    compile 'com.squareup:android-times-square:1.6.4@aar'
    compile 'com.roomorama:caldroid:3.0.1'
    compile 'com.github.sundeepk:compact-calendar-view:1.8.4'

    // Tab strip
    //compile 'com.ogaclejapan.smarttablayout:library:1.1.0@aar'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'

    // Event bus
    compile 'de.greenrobot:eventbus:2.2.1'

    // qr scanning
    compile 'com.journeyapps:zxing-android-embedded:3.4.0@aar'
    //compile 'com.journeyapps:zxing-android-integration:2.0.1@aar'
    compile 'com.google.zxing:core:3.0.1'

    // ormlite
    compile 'com.j256.ormlite:ormlite-android:4.45'

    // testing support, to avoid version problems
    androidTestCompile "com.android.support:appcompat-v7:$libVersions.android.support"
    androidTestCompile "com.android.support:recyclerview-v7:$libVersions.android.support"
    androidTestCompile "com.android.support:support-v4:$libVersions.android.support"
    androidTestCompile "com.android.support:design:$libVersions.android.support"
    androidTestCompile "com.android.support:support-annotations:$libVersions.android.support"

    // testing
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test:rules:0.5'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"

    compile 'com.getbase:floatingactionbutton:1.9.0'

    // google rfc 2445
    compile files('libs/rfc2445.jar')

    compile('com.mikepenz:materialdrawer:4.4.1@aar') {
        transitive = true
    }
    compile 'com.mikepenz:iconics-core:2.7.1@aar'
    compile 'com.mikepenz:google-material-typeface:2.1.2.1@aar'
    compile 'com.mikepenz:community-material-typeface:1.2.65.1@aar'
    compile('com.mikepenz:materialize:0.2.7@aar') {
        transitive = true
    }
    compile('com.mikepenz:aboutlibraries:5.2.5@aar') {
        transitive = true
    }

    //
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.heinrichreimersoftware:material-intro:1.5.8'

    // html parsing
    compile 'org.jsoup:jsoup:1.7.2'

    // material dialogs
    compile 'com.github.javiersantos:MaterialStyledDialogs:2.0'

    // jobs
    compile 'com.evernote:android-job:1.1.3'

    // recyclerview adapters
    compile('com.mikepenz:fastadapter:2.1.5@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fastadapter-commons:2.1.0@aar'
    compile 'com.mikepenz:fastadapter-extensions:2.1.0@aar'

    // levenshtein distance etc
    compile 'org.apache.commons:commons-lang3:3.5'

    // gif imageview
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.3'
    // retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'

    // google maps
    compile 'com.google.android.gms:play-services-maps:9.8.0'
    compile 'com.google.android.gms:play-services-location:9.8.0'
    compile 'com.google.android.gms:play-services-places:9.8.0'
    compile 'com.google.maps.android:android-maps-utils:0.4+'

    // slide up panel
    compile 'com.sothree.slidinguppanel:library:3.0.0'

    // memory leak debugging
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    alphaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    betaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    androidTestCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'


}

if(currentFlavor == "pharma"){
    apply plugin: 'com.google.gms.google-services'
}


